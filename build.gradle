apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']
sourceSets.test.java.srcDirs = ['test']
sourceSets.test.resources.srcDirs = ['resources']

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

dependencies {
    compileOnly group: 'net.sf.kxml', name: 'kxml2', version: '2.3.0'
    
    testCompile group: 'junit', name: 'junit', version: '3.8.2'
    testCompile group: 'net.sf.kxml', name: 'kxml2', version: '2.3.0'
}

jar {
    baseName = 'opendatakit-javarosa'
    version = '2.1.0'
    archiveName = baseName + '-' + version + '.jar'

    manifest {
        attributes 'Manifest-Version': "$jar.version"
    }
}

// TODO: does not build UML diagrams
javadoc {
    failOnError = false
}

group = 'org.opendatakit'
archivesBaseName = "$jar.baseName"
version = "$jar.version"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name "$jar.baseName"
        packaging 'jar'
        artifactId "$jar.baseName"
        description 'A Java library for rendering forms that are compliant with ODK XForms spec'
        url 'http://github.com/opendatakit/javarosa'

        scm {
          connection 'scm:git:git://github.com/opendatakit/javarosa.git'
          developerConnection 'scm:git:ssh://github.com:opendatakit/javarosa.git'
          url 'http://github.com/opendatakit/javarosa/tree/master'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'opendatakit'
            name 'Open Data Kit'
            email 'opendatakit.org@gmail.com'
          }
        }
      }
    }
  }
}